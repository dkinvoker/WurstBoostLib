package BonEffectInstance

import LinkedList
import BonusInstance
import Status
import ClosureTimers

public class BonEffectInstance
    private LinkedList<BonusInstance> bonuses = new LinkedList<BonusInstance>()
    private Status bonEffect
    private CallbackSingle endOfBonCallback

    construct(Status bon, LinkedList<BonusInstance> bonuses)
        this.bonEffect = bon
        this.bonuses = bonuses

    function getBonEffect() returns Status
        return this.bonEffect

    /**
    Attaches callback function that will be called after duration to this BonEffectInstance.
    Remember that callback will be triggered even if not binded with BonEffectInstance, however binding is necessary to no lose its reference, and be able to disable it if needed.
    */
    function bindEndOfBonCallback(CallbackSingle callback)
        this.endOfBonCallback = callback

    /**
    Remove all of BonusInstances from target. 
    Do not destroy endOfBonCallback.
    Can be ussed in endOfBonCallback itself.
    */
    function removeBonuses(unit u)
        for bonus in this.bonuses
            bonus.removeEffect(u)

    /**
    Remove all of BonusInstances from target, and destroys endOfBonCallback.
    Cannot be used inside endOfBonCallback function! 
    It would destroy callback during its execution causing errors and wurst memory corruption.
    */
    function dispell(unit u)
        this.removeBonuses(u)
        this.abortCallback()

    /**
    Destroys endOfBonCallback causing it to not be fired.
    */
    function abortCallback()
        destroy this.endOfBonCallback

    ondestroy
        bonuses.forEach() (BonusInstance t) ->
            destroy t
        destroy bonuses